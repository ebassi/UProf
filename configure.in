dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

dnl ================================================================
dnl Package version (i.e. the pretty number that users see)
dnl ================================================================
m4_define([uprof_major_version], [0])
m4_define([uprof_minor_version], [2])
m4_define([uprof_micro_version], [0])
m4_define([uprof_version],
          [uprof_major_version.uprof_minor_version.uprof_micro_version])

dnl ================================================================
dnl Interface version details for libtool
dnl ================================================================
# NB: I didn't copy the widely used glib/gtk style of dealing with libtool
# versioning since I found the different (compared to libtool's documentation)
# ideas for what the interface age should represent; and the addition of a
# binary age confusing. I couldn't find any documentation/mailing list archives
# etc explaing the ideas nor any code that makes use of those values. They also
# seem to go against the advice of not basing the libtool versioning on package
# versioning.
#
# For reference on how the various numbers should be updated at release time
# these rules are adapted from the libtool info pages:
#
#  1. Update the version information only immediately before a public
#     release of your software.
#
#  2. If the library source code has changed at all since the last
#     update, then increment REVISION (`C:R:A' becomes `C:r+1:A').
#
#  3. If any interfaces have been added, removed, or changed since the
#     last update, increment CURRENT, and set REVISION to 0.
#
#  4. If any interfaces have been added since the last public release,
#     then increment AGE.
#
#  5. If any interfaces have been removed since the last public release,
#     then set AGE to 0.
m4_define([uprof_lt_current], 1)
m4_define([uprof_lt_revision], 0)
m4_define([uprof_lt_age], 0)
# We do also tell libtool the pretty version also:
m4_define([uprof_lt_release], [uprof_version])

AC_INIT(uprof,  [uprof_version])
AC_CONFIG_SRCDIR(uprof/uprof.c)
AC_CONFIG_AUX_DIR([autotools])
AC_CONFIG_MACRO_DIR([autotools/m4])
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE([gnu])


dnl ================================================================
dnl More version number stuff
dnl ================================================================
UPROF_MAJOR_VERSION=uprof_major_version
UPROF_MINOR_VERSION=uprof_minor_version
UPROF_MICRO_VERSION=uprof_micro_version
UPROF_VERSION=uprof_version

AC_SUBST(UPROF_MAJOR_VERSION)
AC_SUBST(UPROF_MINOR_VERSION)
AC_SUBST(UPROF_MICRO_VERSION)
AC_SUBST(UPROF_VERSION)


dnl ================================================================
dnl libtool versioning
dnl ================================================================
UPROF_LT_CURRENT=uprof_lt_current
UPROF_LT_REVISION=uprof_lt_revision
UPROF_LT_AGE=uprof_lt_age
UPROF_LT_RELEASE=uprof_lt_release
AC_SUBST(UPROF_LT_CURRENT)
AC_SUBST(UPROF_LT_REVISION)
AC_SUBST(UPROF_LT_AGE)
AC_SUBST(UPROF_LT_RELEASE)


dnl ================================================================
dnl Handle extra configure options
dnl ================================================================

AC_ARG_ENABLE(debug,
	      AC_HELP_STRING([--enable-debug], [enable debuging features]))
if test x"$enable_debug" = xyes; then
 AC_DEFINE(UPROF_DEBUG, 1, "define to enable debuging")
 CFLAGS=""
 EXTRA_CFLAGS="-g3 -O0"
fi

AC_ARG_ENABLE(maintainer-flags,
	      AC_HELP_STRING([--enable-maintainer-flags],
			     [Use strict compiler flags for compilation]))
if test x"$enable_maintainer_flags" = xyes; then
  EXTRA_CFLAGS+=" \
-Werror -Wall -Wshadow -Wcast-align -Wno-strict-aliasing \
-Wno-uninitialized -Wempty-body -Wformat-security -Winit-self"
else
  EXTRA_CFLAGS+=" -Wall"
fi

dnl ================================================================
dnl Compiler stuff.
dnl ================================================================
AM_PROG_CC_C_O
AC_ISC_POSIX
AC_C_CONST


dnl ================================================================
dnl Libtool stuff.
dnl ================================================================
AC_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --preserve-dup-deps"


dnl ================================================================
dnl I18n stuff.
dnl ================================================================
dnl AC_PROG_INTLTOOL([0.23])
dnl GETTEXT_PACKAGE=uprof
dnl AC_SUBST(GETTEXT_PACKAGE)
dnl AC_SUBST([CONFIG_STATUS_DEPENDENCIES],['$(top_srcdir)/po/LINGUAS'])
dnl AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package])
dnl ALL_LINGUAS="`cat "$srcdir/po/LINGUAS" | grep -v '^#'`"
dnl uproflocaledir='${prefix}/${DATADIRNAME}/locale'
dnl AC_SUBST(uproflocaledir)
dnl AM_GLIB_GNU_GETTEXT


dnl ================================================================
dnl Documentation stuff.
dnl ================================================================
GTK_DOC_CHECK(1.4)
GNOME_DOC_INIT


dnl ================================================================
dnl Check for dependency packages.
dnl ================================================================
UPROF_PKG_REQUIRES="glib-2.0 gobject-2.0 dbus-glib-1"
AC_SUBST(UPROF_PKG_REQUIRES)
PKG_CHECK_MODULES(UPROF_DEP, [$UPROF_PKG_REQUIRES])


dnl ================================================================
dnl Misc program dependencies.
dnl ================================================================
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PROG_INSTALL


dnl ================================================================
dnl Checks for header files.
dnl ================================================================
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h signal.h)


dnl ================================================================
dnl Checks for library functions.
dnl ================================================================
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(putenv strdup)

AC_CHECK_LIB(ncursesw, [subwin],
             [HAVE_NCURSES=yes],
             [HAVE_NCURSES=no])

AM_CONDITIONAL(HAVE_NCURSES, [test "x$HAVE_NCURSES" = "xyes"])

dnl ================================================================
dnl What needs to be substituted in other files
dnl ================================================================
AC_SUBST(EXTRA_CFLAGS)
AC_SUBST(EXTRA_CPPFLAGS)
AC_SUBST(UPROF_DEP_CFLAGS)
AC_SUBST(UPROF_DEP_LIBS)


dnl ================================================================
dnl Dah Da!
dnl ================================================================
SHAVE_INIT([autotools], [enable])
AC_OUTPUT(
Makefile
autotools/shave
autotools/shave-libtool
uprof/Makefile
tests/Makefile
doc/Makefile
doc/reference/Makefile
doc/reference/uprof/Makefile
doc/reference/uprof/version.xml
uprof-$UPROF_MAJOR_VERSION.$UPROF_MINOR_VERSION.pc:uprof.pc.in
dnl po/Makefile.in
)

